buildscript {
  // ---- DEPENDENCIES ----
  // @formatter:off
  ext.buildConfig = [
      compileSdk              : 28,
      targetSdk               : 28,
      minSdk                  : 14,
      gradlePlugin            : '3.4.0'
  ]

  ext.versions = [
      kotlin                  : '1.3.30',
      androidx                : '1.1.0-alpha05',
      preference              : '1.1.0-alpha04',
      constraintlayout        : '2.0.0-alpha4',
      colorpicker             : '1.0.5',
      junit                   : '4.12',
      mockito_kotlin          : '2.0.0',
      mockito_core            : '2.23.4'
  ]

  ext.deps = [
      'androidx': [
          'appCompat'         : "androidx.appcompat:appcompat:${versions.androidx}",
          'constraintLayout'  : "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}",
          'preferences'       : "androidx.preference:preference-ktx:${versions.preference}"
      ],
      'kotlin': [
          'stdlib': [
              'jdk'           : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
          ]
      ],
      'material'              : "com.google.android.material:material:${versions.androidx}",
      'colorpicker'           : "com.jaredrummler:colorpicker-compat:${versions.colorpicker}",
      'junit'                 : "junit:junit:${versions.junit}",
      'mockito': [
          'core'              : "org.mockito:mockito-core:${versions.mockito_core}",
          'kotlin'            : "com.nhaarman.mockitokotlin2:mockito-kotlin:${versions.mockito_kotlin}"
      ]
  ]
  // @formatter:on

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${buildConfig.gradlePlugin}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        if (details.requested.group == 'org.jetbrains.kotlin') {
          details.useVersion versions.kotlin
        }
      }
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
      allWarningsAsErrors = true
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
